## 练习3：分析bootloader进入保护模式的过程。（要求在报告中写出分析）

实验手册既然提升了阅读lab1/boot/bootasm.S源码，那么我们直接对照源码静态分析即可。

这段代码是如何加载进内存的？既然是在操作系统加载前就需要进入保护模式，那么显然是由BIOS加载的。BIOS将这段代码从硬盘中读出并加载到内存中物理地址为0x7c00的位置，并在实模式下运行这段代码(`%cs=0 %ip=7c00`)。

    code16                                             # Assemble for 16-bit mode
        cli                                             # Disable interrupts
        cld                                             # String operations increment

        # Set up the important data segment registers (DS, ES, SS).
        xorw %ax, %ax                                   # Segment number zero
        movw %ax, %ds                                   # -> Data Segment
        movw %ax, %es                                   # -> Extra Segment
        movw %ax, %ss                                   # -> Stack Segment

首先用`cli`指令禁止中断发生，然后用`cld`使DF复位，即使块的传送方向从低地址到高地址。然后将段寄存器置为0。

接下来bootloader需要**开启A20**。

### 什么是A20？

在8086中，只有20根地址总线,所以可以访问的地址是2^20=1M，但是由于是16位地址模式，能表示的地址范围只有64K，所以为了能访问1M内存，Intel采用了分段的模式：16位基地址+16位的偏移量，绝对地址的计算方法是16位基地址\*4+16位偏移=20位地址。

通过上述分段模式,能够表示的最大内存为:

`FFFFh:FFFFh=FFFF0h+FFFFh=10FFEFh=1M+64K-16Bytes(1M多余出来的部分被称做高端内存区HMA)`

但是8086只有20位地址线，如果访问100000h\~10FFEFh之间的内存必须有第21根总线。所以当程序员给出超过1M的地址时，系统并不认为其访问越界而产生异常,而是自动从重新0开始计算,也就是说系统计算实际地址的时候是按照对1M求模的方式进行的。但是到了80386，Intel提出需要对下兼容，即80386在实模式下系统行为与8086一致，但是80386中系统地址总线有32位，意味着如果程序员访问100000H-10FFEFH之间的内存,系统将实际访问这块内存。

为了解决上述问题,IBM使用键盘控制器上剩余的一些输出线来管理第21根地址线(从0开始数是第20根),被称为A20 Gate。如果A20 Gate被打开，则当程序员给出100000H-10FFEFH之间的地址的时候，系统将真正访问这块内存区域；如果A20 Gate被禁止,则当程序员给出100000H-10FFEFH之间的地址的时候，系统仍然使用8086的方式。

A20,其实它就是对于20-bit(从0开始数)的特殊处理(也就是对第21根地址线的处理)，即决定第20位bit是恒为0，还有既可以为1也可以为0.所以在保护模式下，如果A20 Gate被禁止，则可以访问的内存只能是奇数1M段，即1M,3M,5M…，也就是00000-FFFFF，200000-2FFFFF，400000-4FFFFF…...如果A20 Gate被打开，则可以访问的内存则是连续的。

所以开启A20的含义就是使得地址总线上第20位bit可用。

### 如何启动A20

实际上A20是由一个8042键盘控制器来控制的A20 Gate，如图所示，A20 Gate一直处于0，即使A20 line是1，在经过与门后，得到的地址第20位bit还是0。

![image](https://img1.imgtp.com/2022/10/09/KrOIsr6I.jpg)

所以启动A20就是将8042的这个端口置为1。代码含义如下:

    seta20.1:
        inb $0x64, %al                                  
        testb $0x2, %al				#将当前0x64端口的状态读入al，并进行检查
        jnz seta20.1				#等待端口状态为可以写入

        movb $0xd1, %al             #将0xd1写入port 0x64中
        outb %al, $0x64             #0xd1的含义是:准备向8042的P2端口写入

    seta20.2:
        inb $0x64, %al              
        testb $0x2, %al
        jnz seta20.2

        movb $0xdf, %al             #将0xdf写入port 0x60中
        outb %al, $0x60             #0xdf的含义是让8042打开A20 Gate

### 什么是GDT表

全局描述符表GDT（Global Descriptor Table）在整个系统中，全局描述符表GDT只有一张(一个处理器对应一个GDT)。\*\*\*GDT主要的作用是将逻辑地址映射为线性地址。\*\*\*GDT可以被放在内存的任何位置，但CPU必须知道GDT的入口，也就是基地址放在哪里。为此，Intel的设计者门提供了一个寄存器GDTR用来存放GDT的入口地址，程序员将GDT设定在内存中某个位置之后，可以通过`LGDT`指令将GDT的入口地址装入此积存器，从此以后，CPU就根据此寄存器中的内容作为GDT的入口来访问GDT了。GDTR中存放的是GDT在内存中的基地址和其表长界限。

GDT中每一个元素是一个描述符(64位)，包含了段基址、段长度、属性这些信息。而原来的段寄存器存储的值不再是段偏移了，而是GDT的索引，通过这个索引找到对应的描述符从而求出线性地址。

**\*Example--给出逻辑地址cs\:offset，根据cs中的索引找到GDT表中对应的描述符，假设该描述符描述的段基址是base，那么线性地址就是base+offset。*****++关于LDT等问题暂不叙述，这里只做一个简单介绍。++***

    	lgdt gdtdesc				#初始化GDT表

接下来启动保护模式：

        movl %cr0, %eax				#通过将cr0寄存器的PE位置为1开启保护模式
        orl $CR0_PE_ON, %eax
        movl %eax, %cr0

启动完保护模式后需要通过一个长跳转指令来更新cs寄存器的基址。因为进入保护模式后，cup切换置32位处理模式，以后的寻址便会基于我们刚刚设置的GDT表来进行。CPU会读取我们CS寄存器的值作为段选择子来按照上述方法寻址，然而此时CS寄存器的值为0，我们若不重新设置CS寄存器则会定位到空段描述符（GDT表的第一项），寻址时会导致错误。使用ljmp指令设CS寄存器的值，使其作为段选择子定位到我们刚刚设的GDT表中代码段的描述符。PROT\_MODE\_CSEG定义为0x8，即将0x8写入CS寄存器，并跳转到protcseg代码段。

接下来我们将其他5个段寄存器填入数据段的段选择子，之后它们访问数据，将会得到我们在GDT表中设置的数据段描述符所描述的属性。接下来设置C程序环境，代码中将0x0000\~0x7c00的这段连续的内存空间作为bootloader后续要使用的栈。

其实在汇编代码最开始的部分已经声明好了，进入保护模式后的段寄存器的选择子如何更新。

    .set PROT_MODE_CSEG, 0x8
    .set PROT_MODE_DSEG, 0x10



    	ljmp $PROT_MODE_CSEG, $protcseg					#更新cs的基地址		

    .code32                                             
    protcseg:
    	#将各个段寄存器的值设为0x10
        movw $PROT_MODE_DSEG, %ax
        movw %ax, %ds                                   # -> DS: Data Segment
        movw %ax, %es                                   # -> ES: Extra Segment
        movw %ax, %fs                                   # -> FS
        movw %ax, %gs                                   # -> GS
        movw %ax, %ss                                   # -> SS: Stack Segment

        movl $0x0, %ebp
    	#esp指向0x7c00
        movl $start, %esp

启动保护模式操作完成后调用boot主方法，如果bootmain发生异常会在一个spin段进行循环。

        call bootmain

    spin:
        jmp spin

